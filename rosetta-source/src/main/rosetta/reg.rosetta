namespace "regulatorybodypoc"
version "${project.version}"

body Authority RegulatoryBodyPoc <"The Regulatory Body POC (RBP).>

body Authority RegulationAuthority <"The PRA Rulebook contains provisions made by the PRA that apply to PRA-authorised firms">

body Authority FinancialConductAuthority <"The Financial Conduct Authority is the conduct regulator for nearly 60,000 financial services firms and financial markets in the UK and the prudential supervisor for 49,000 firms, setting specific standards for 19,000 firms.">

body Authority EuropeanBankingAuthority <"European Banking Authority (europa.eu).">

body Authority HMTreasury <"HM Treasury is the governments economic and finance ministry, maintaining control over public spending, setting the direction of the UKs economic policy and working to achieve strong and sustainable economic growth.">

corpus Act "FSMA 2000 (Regulated Activities) Order 2001" FSMA_2000_Regulate_Activities_Order_2001
    <"https://www.legislation.gov.uk/uksi/2001/544/contents/made">

corpus Act "FSMA 2000" FSMA_2000
    <"https://www.legislation.gov.uk/ukpga/2000/8/contents">

corpus Act "FSMA 2000 (PRA-regulated Activities) Order (S.I. 2013/556)" FSMA_2000_PRA_regulated_Activities_Order_S_I_2013_556
    <"https://www.legislation.gov.uk/uksi/2013/556">

corpus Act "Building societies Act 1986" BuildingSocietiesAct1986
    <"https://www.legislation.gov.uk/ukpga/1986/53/contents">

corpus Act "Companies Act 2006" CompaniesAct2006
    <"https://www.legislation.gov.uk/ukpga/2006/46/contents/enacted">

corpus Act "Credit Unions Act 1979" CreditUnionsAct1979
    <"https://www.legislation.gov.uk/ukpga/1979/34/contents">

corpus Directive "Directive 86/635/EEC" Directive86_635_EEC
    <"https://eur-lex.europa.eu/legal-content/EN/ALL/?uri=CELEX:31986L0635">

corpus Directive "Directive 2006/48/EEC" Directive2006_48_EEC
    <"https://eur-lex.europa.eu/legal-content/EN/TXT/?uri=CELEX%3A32006L0048">

corpus Regulation "Capital Requirements Regulation (CRR) | European Banking Authority (europa.eu)" CRR
    <"https://www.eba.europa.eu/regulation-and-policy/single-rulebook/interactive-single-rulebook/504">

corpus Regulation "Regulation (EU) No 1093/2010" RegulationEUNo1093_2010
    <"https://eur-lex.europa.eu/legal-content/EN/TXT/?uri=CELEX:32010R1093">

corpus Regulation "PRA Rule Book" DefinitionOfCapital
    <"http://www.prarulebook.co.uk/rulebook">

segment rule_url
segment chapter_url
segment part_url
segment sector_url
segment rulebook_url

segment rule_id
segment chapter_name
segment part_name
segment sector_name

segment rule_col
segment rulebook_date
segment rule_date
segment rule_number

report RegulationAuthority CRR DefinitionOfCapital in real-time
    when IsBank
    with fields
        SmallSpecialistBank
        OwnFunds
        

eligibility rule IsBank
    filter when LegalEntity -> bank exists

reporting rule SmallSpecialistBank
    [regulatoryReference RegulationAuthority CRR DefinitionOfCapital
        sector_name "CRR Firms"
        part_name "Definition of Capital"
        chapter_name "Application and Definitions"
        rule_id "211300"
        rule_col "1.2"
        rulebook_date "16/11/2019"
        rule_date "01/01/2014"
        rule_number "1.2"
        rule_url "http://www.prarulebook.co.uk/rulebook/Content/Chapter/211286/16-11-2019#211300"
        provision "In this Part the following definitions shall apply:
                    Small specialist bank
                    a bank that has capital resources equal to or in excess of the base capital resources requirement for a small specialist bank in 12.1 but less than the base capital resources requirement of a bank and that carries out one or more of the following activities:
                    (1) provides current and savings accounts;
                    (2) lending to small and medium-sized enterprises;
                    (3) lending secured by mortgages on residential property."]
                filter when rule MeetActivitiesRequirement then
                filter when rule MeetsBaseCapitalResourcesRequirement then 
                return "Yes" as "Small Specialist Bank"


reporting rule MeetActivitiesRequirement
    filter when LegalEntity -> bank exists then
       extract
       (
            (
                LegalEntity -> bank -> activity -> lending -> clientType = ClientType -> SmallEnterprise
                    or
                LegalEntity -> bank -> activity -> lending -> clientType = ClientType -> MediumEnterprise
            )
        or 
            (
            LegalEntity -> bank -> activity -> lending -> collateralType = CollateralType -> ResidentialProperty
                and
            LegalEntity -> bank -> activity -> lending -> asset = AssetType -> Mortgage
            )
        // This is the correct logic but does not work due to code generator bug
        //  or LegalEntity -> bank -> activity -> bankAccount contains [BankAccountActivity -> SavingsAccount, BankAccountActivity -> CurrentAccount]
        or  // This is alternative logic that gives the same result
            (
                LegalEntity -> bank -> activity -> bankAccount <> BankAccountActivity -> CurrentAccount and LegalEntity -> bank -> activity -> bankAccount <> BankAccountActivity -> SavingsAccount and LegalEntity -> bank -> activity -> bankAccount exists
            )
       )

        

reporting rule MeetsBaseCapitalResourcesRequirement
    [regulatoryReference RegulationAuthority CRR DefinitionOfCapital
        sector_name "CRR Firms"
        part_name "Definition of Capital"
        chapter_name "Application and Definitions"
        rule_id "211366"
        rule_col "12.1"
        rulebook_date "16/11/2019"
        rule_date "01/01/2014"
        rule_number "12.1"
        rulebook_url "http://www.prarulebook.co.uk/rulebook/Home/Rulebook/16-11-2019"
        sector_url "http://www.prarulebook.co.uk/rulebook/Content/Sector/211131/16-11-2019"
        part_url "http://www.prarulebook.co.uk/rulebook/Content/Part/211285/16-11-2019"
        chapter_url "http://www.prarulebook.co.uk/rulebook/Content/Chapter/211286/16-11-2019"
        rule_url "http://www.prarulebook.co.uk/rulebook/Content/Chapter/211286/16-11-2019#211300"
        provision "A CRR firm must maintain at all times capital resources equal to or in excess of the base capital resources requirement set out in the table below:
                Firm categoryAmount: Currency equivalent bank 5 million
                small specialist bank - The higher of 1 million and 1 million
                building society - The higher of 1 million and 1 million
                designated investment firm 730,000"]

    filter when LegalEntity -> bank exists then
    filter when LegalEntity -> bank -> financials -> balanceSheet -> liability -> liabilityType = LiabilityType -> RetainedEarnings then
        extract LegalEntity -> bank -> financials -> balanceSheet -> liability then
        extract Liability -> value < 5000000.0 and Liability -> value >= 1000000.0

reporting rule OwnFunds 
    filter when LegalEntity -> bank -> financials -> balanceSheet -> liability -> liabilityType = LiabilityType -> RetainedEarnings then
    extract LegalEntity -> bank -> financials -> balanceSheet -> liability -> value as "Own Funds"
